# survival.bugs.primary.txt
# version for JAGS
# Dec 2017
model{
    for (i in 1:N){
	  isCensored[i] ~ dinterval(LOS[i], TIME_CEN[i])
	  squared.res[i] <- pow(LOS[i] - mu[i], 2) 
      LOS[i] ~ dweib(shape, mu[i])
 	  log(mu[i]) <- beta[1] + beta[2]*age[i] + beta[3]*gender[i] + beta[4]*ADL[i] + beta[5]*SPMSQ[i] + beta[6]*CCI[i] + beta[7]*intervention[i] + zeta.c[ward.num[i]]
	}
    shape ~ dexp(0.001)
    for (k in 1:n.beta){
      beta[k] ~ dnorm(0, 0.001)
    }
    for (k in 1:n.ward){
      zeta[k] ~ dnorm(0, tau.ward)
      zeta.c[k] <- zeta[k] - mu.zeta
    }    
    tau.ward ~ dgamma(1, 1)
    mu.zeta <- mean(zeta[1:n.ward])
    # Median times
    median[1] <- pow(log(2) * exp(-beta[1]), 1/shape); # baseline median
    for (k in 2:n.beta){
      median[k] <- pow(log(2) * exp(-(beta[1]+beta[k])), 1/shape);
      diff[k-1] <- median[k] - median[1];
    }
	# Mean residual sum of squares
	rss <- sum(squared.res[1:N])/N
}
